; args: none
; delay to close options dialog
~0.1

_dlg = createDialog "TZK_Setting_Dialog"
_si = siPlayer
btnAction0 = false

_lastMissionSettings = [ [], [], [] ]; _booleMatrix = [boolSettingGUI, boolSettingEnvironments, boolSettingControls]

[_lastMissionSettings, _booleMatrix, "missionSettingMatrix_2"] call loadFile "Player\Dialog\TZK_Mission_Setting_Init.sqf"

; Show text in dev for debugging
? !dev : _i = 1; _c = 3; while "_i <= _c" do { ctrlShow [IDC + _i, false]; _i = _i + 1 }
ctrlSetText [IDC + 10, "  TZK Mission Setting 2"]; ctrlSetText [IDC + 11, "  GUI"]; ctrlSetText [IDC + 12, "  Environments"]; ctrlSetText [IDC + 13, "  Controls"]

#Update
ctrlShow [IDC_DialogBG_CR, !bool_TZK_DarkSkin]; ctrlShow [IDC_DialogBG_Light, bool_TZK_DarkSkin]
	? !(dialog): goto "Quit"
	? !(alive player): closeDialog 0; goto "Quit"

	_i = 0; _Column = 3
	#LoopColumn
	_idc = IDC + 100 + 100*_i; _last = _lastMissionSettings select _i; _booleans = _booleMatrix select _i
	_j = 0; _Line = 12
		#LoopLine
		? lbCurSel (_idc + _j) != (_last select _j) && lbCurSel (_idc + _j) >= 0: call ((_booleans select _j) + format ["= %1", [false, true] select lbCurSel (_idc + _j)]); _last set [_j, lbCurSel (_idc + _j)]
		_j = _j + 1
		? _j < _Line : goto "LoopLine"
	_i = _i + 1
	? _i < _Column : goto "LoopColumn"
	
	? btnAction0: btnAction0 = false, [] exec {Player\Dialog\TZK_Mission_Setting_1.sqs}, closeDialog 0, goto "Quit"

	~0.2
	goto "Update"

#Quit
; save booleans for player
; use original OFP magazines/weapons to record
_soldier = "SoldierWB" camCreate [10,10,10]

	_i = 0; _Column = 3
	#BoolesColumn
	removeAllWeapons _soldier
	_booleans = _booleMatrix select _i
	_j = 0; _Line = 12
		#BoolesLine
		? _j < (count _booleans) : if ( call (_booleans select _j) ) then { _soldier addMagazine (magazinesBooleRecording select _j) }
		_j = _j + 1
		? _j < _Line : goto "BoolesLine"
		
	_entry = format["TZK_Mission_Setting_2_%1", _i]
	; if (count magazines _soldier > 0) Then {_soldier saveStatus _entry} Else {deleteStatus _entry}
	_soldier addMagazine (magazinesBooleRecording select count magazinesBooleRecording - 1); _soldier saveStatus _entry
		
	_i = _i + 1
	? _i < _Column : goto "BoolesColumn"

	deleteVehicle _soldier

; EOF