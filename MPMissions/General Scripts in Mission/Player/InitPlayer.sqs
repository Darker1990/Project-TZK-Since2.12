; args: [unit, si, gi]
; Since TZK_3.00 and TZK_2.12 common functions/message handlers for both player and spectator are intergraded in "\TZK_Scripts_4_0_4\Player\InitClient.sqs"

_unit = _this select 0
_si = _this select 1
_gi = _this select 2

forceMap true

;init player functions
funcCalcAlignPosDir = loadFile "\TZK_Scripts_4_0_4\Player\SQF\CalcAlignPosDir.sqf"
; // args: [si, type, pos, dir]
; // return: [pos, dir]
funcCalcUID = loadFile "\TZK_Scripts_4_0_4\Player\SQF\CalcUID.sqf"
; // args: [unit, si, gi]
; // return: UID

funcGetOrderDesc = loadFile "\TZK_Scripts_4_0_4\Player\SQF\GetOrderDesc.sqf"
funcGetOrderTempDesc = loadFile "\TZK_Scripts_4_0_4\Player\SQF\GetOrderTempDesc.sqf"
; // arguments: _order
; // returns: string
funcGetQueueLength = loadFile "\TZK_Scripts_4_0_4\Player\SQF\GetQueueLength.sqf"
; // arguments: obj
; // returns: length

funcMapClickPlayer = loadFile "Player\SQF\MapClickPlayer.sqf"
; // arguments: [_pos, _units, _alt, _shift]
; // returns: true if command processed, false if not
funcSideMsg = loadFile "\TZK_Scripts_4_0_4\Player\SQF\SideMsg.sqf"
; // args: text
funcMove = loadFile "\TZK_Scripts_4_0_4\Player\SQF\Move.sqf"
; // args: [unit, pos]
; // return: none

funcPosBullCam = loadFile "\TZK_Scripts_4_0_4\Player\SQF\PosBullCam.sqf"
funcMappingUnitsPlayer = loadFile "\TZK_Scripts_4_0_4\Player\Marker\MappingUnitsPlayer.sqf"
? dev : funcMappingPlayerDev = loadFile "\TZK_Scripts_4_0_4\Player\Marker\MappingPlayerDev.sqf"
funcHideMarkers = loadFile "\TZK_Scripts_4_0_4\Player\Marker\HideMarkers.sqf"

funcCheckWhetherSuperior = loadFile "\TZK_Scripts_4_0_4\Player\SQF\CheckWhetherSuperior.sqf"
; // args: gis
; // return: player is superior or not

funcGetWorkingStructuresWithinCCRange = loadFile "\TZK_Scripts_4_0_4\Player\SQF\GetWorkingStructuresWithinCCRange.sqf"
; // args: [si, structType] (e.g. [si0, stBarracks])
; // return: [struct, struct, ...]


structAction = -1
lastSelectedStructType = -1
lastSelectedUnitType = -1

aVehiclesWithRepairAction = []
vehiclesRearming = []
giMarkersAI = count (groupMatrix select _si)

qPlayer = []
qLen = []; mutexqLen = false
lastSelectedFactory = [ [], [] ]

timeLastPing = 30
bBlockedByClient = false

bMannedDriver = true
bMannedGunner = true
bMannedCommander = false


alignWalls = false

respawnType = 10
objectRespawn = objNull

playerSide = side _unit
groupPlayer = group _unit
siPlayer = _si
giPlayer = _gi

unitcamUnit = objNull
iView = 0
satcamUnit = objNull
satcamPos = []
satcamAlt = viewDistance/4
SpySatPos0 = []; SpySatPos1 = []; bSpySat = false

reportAll = 0
reportVehiclesStructs = 1
reportStructs = 2
reportNone = 3
report = reportAll

Destruction_Undo0 = false
Destruction_Undo1 = false

wpPlayer = []
_i = 0; while "_i < countWPPlayer" do { wpPlayer set [_i, [-1,-1]]; _i = _i + 1 } 
timeLastWaypointSet = 0
indexWayPointSelected = -1


mutexBuildStructure = false; mutexOrderBuildStructure = false
mutexMoneySpent = false
mutexSendCancelBuy = false; mutexqPlayer = false
indexSendBuyUnit = 0; countSendBuyUnit = 4

; MESSAGE HANDLERS (PLAYERS)
mutexMoneyStatus = false; _i = 0; while "_i < 10" do {call format [{[] exec "\TZK_Scripts_4_0_4\Player\Msg\mMoneyStatus%1.sqs"}, _i]; _i = _i + 1}
[] exec "\TZK_Scripts_4_0_4\Player\Msg\mMoneyTransferred.sqs"

_i = 0; _count = 10; while "_i < _count" do {call Format[{[] exec "\TZK_Scripts_4_0_4\Player\Msg\mAddUnit%1.sqs"}, _i]; _i = _i + 1}

_i = 0; _count = 5; while "_i < _count" do {call Format[{[] exec "\TZK_Scripts_4_0_4\Player\Msg\mQueueAdd%1.sqs"; [] exec "\TZK_Scripts_4_0_4\Player\Msg\mQueueRem%1.sqs"}, _i]; _i = _i + 1}
[] exec "\TZK_Scripts_4_0_4\Player\Msg\mQueueLength.sqs"

[] exec "\TZK_Scripts_4_0_4\Player\Msg\mMovingToSupport.sqs"

? _si == si0: [] exec "\TZK_Scripts_4_0_4\Player\Msg\mEnemySpotted0.sqs"
? _si == si1: [] exec "\TZK_Scripts_4_0_4\Player\Msg\mEnemySpotted1.sqs"

[] exec "\TZK_Scripts_4_0_4\Player\Msg\mPingServer.sqs"

_targetObject = {Target_TZK_xj400}
; ? bool_TZK_Vanilla_Mode: _targetObject = "HeliH"
_i = 0; countTargetMarkers = 10; while "_i < countTargetMarkers" do {call Format[{TargetP_%1 = _targetObject camCreate [0,0,0]; TargetP_%1 exec "\TZK_Scripts_4_0_4\Common\Command\allowDamageFalse.sqs"}, _i]; _i = _i + 1}
; Should better use camCreate than createVehicle. Its position don't have to be broadcast.

;game has begun after this pause
~0.1

mapAnimClear
mapAnimAdd [0, 1.0, posCenter]
mapAnimCommit
@mapAnimDone

; TODO - disable for release
; player addAction ["TEST", "\TZK_Scripts_4_0_4\Player\Action\Test.sqs"]

onMapSingleClick {[_pos, _units, _alt, _shift] call funcMapClickPlayer}

player groupchat "Waiting for start data ..."
@( !(isNull (mhq select si0)) && !(isNull (mhq select si1)) )
player groupchat "Start data received."

[_unit, _si, _gi] exec "\TZK_Scripts_4_0_4\Common\PlaceGroupLeader.sqs"
[] exec "Player\UpdateOptionsAction.sqs"
[] exec "\TZK_Scripts_4_0_4\Player\UpdatePlayerRepairVehicleAction.sqs"
[] exec "\TZK_Scripts_4_0_4\Player\UpdatePlayerRearmVehicleAction.sqs"
[maxFlightAltitude, maxFlightAltitudePlus] exec "\TZK_Scripts_4_0_4\Player\LimitAltitude.sqs"
[] exec "\TZK_Scripts_4_0_4\Player\UpdatePlayerAirSupportRepairAction.sqs"
[] exec "\TZK_Scripts_4_0_4\Player\UpdatePlayerAirSupportRearmAction.sqs"
[] exec "\TZK_Scripts_4_0_4\Player\UpdatePlayerGetFlyingStatus.sqs"

[] exec "\TZK_Scripts_4_0_4\Player\Start\PlayerClient.sqs"
[] exec "\TZK_Scripts_4_0_4\Player\Start\PlayerVehicle.sqs"

[_si, false] exec "\TZK_Scripts_4_0_4\Player\Marker\PlayerUnits.sqs"; [_si] exec "\TZK_Scripts_4_0_4\Player\Marker\PlayerVehicleEnemy.sqs"; [_si] exec "\TZK_Scripts_4_0_4\Player\Marker\PlayerStruct.sqs"

[] exec "\TZK_Scripts_4_0_4\Player\Msg\mJoinUnits.sqs"


mapAnimClear
mapAnimAdd [1.0, 1.0, getPos (mhq select _si)]
mapAnimCommit
@mapAnimDone
; ~1
; forceMap false

[] exec "\TZK_Scripts_4_0_4\Player\Init\AIOrder.sqs"


; [] exec "\TZK_Scripts_4_0_4\Player\LoadSettings.sqs"
RespawnWeapon = []; RespawnMagazine = []; RespawnAmmunition = [];
RespawnW = []; RespawnM = []; RespawnA = [];
~0.1
[player, siPlayer] exec "\TZK_Scripts_4_0_4\Player\EquipPlayerRespawn.sqs"

; flash start pos
_marker = "Art0"
_size = getMarkerSize _marker
_marker setMarkerSize [1,1]
_i = 0
#FlashStart
_marker setMarkerPos ( [getPos (mhq select _si), hiddenMarkerPos] select ((_i % 2) == 0) )
 ~0.2
 _i = _i + 1
 ? _i < 10 : goto "FlashStart"


 
forceMap false
~0.1
[player, player, false, 0] exec "\TZK_Scripts_4_0_4\Player\Dialog\Equipment.sqs"
~1
@ !dialog
[] exec "\TZK_Scripts_4_0_4\Player\DisplayGameInfo.sqs"
[] exec "\TZK_Scripts_4_0_4\Player\InitCommanderUnit.sqs"
[] exec "\TZK_Scripts_4_0_4\Player\Init\BuyList.sqs"

~15
PlayMusic (Musics0 select music0); 0 FadeMusic 0.35
; Hide start pos marker after some time
~20
20 FadeMusic 0.09
_marker setMarkerPos hiddenMarkerPos
_marker setMarkerSize _size

_westName = []; _groups = groupMatrix select si0; _index = 0; _groupsAI = groupAiMatrix select si0
{ _westName set [_index, format["%1", [name leader _x, "AI"] select (_x in _groupsAI)] ]; _index=_index+1 } forEach _groups;
_eastName = []; _groups = groupMatrix select si1; _index = 0; _groupsAI = groupAiMatrix select si1
{ _eastName set [_index, format["%1", [name leader _x, "AI"] select (_x in _groupsAI)] ]; _index=_index+1 } forEach _groups;
LeaderNameMatrix = [_westName, _eastName]

; EOF