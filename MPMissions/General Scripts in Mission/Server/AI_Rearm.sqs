; args: [unit, si, gi, vehicleRearm, type, support, <non-controlable AI group>, <use comm money>]

_unit = _this select 0
_si = _this select 1
_gi = _this select 2
_vehicleRearm = _this select 3
_type = _this select 4
_support = _this select 5
_nonAIgroup = if (count _this > 6) Then {_this select 6} Else {false}
_bUseCommMoney = if (count _this > 7) Then {_this select 7} Else {false}
_this = nil

? !alive _unit || !alive _vehicleRearm: exit
? _nonAIgroup: if !([_unit, _si, _gi] call funcCheckRearm) Then {exit}
?!_nonAIgroup && _gi == -1: _gi = giCO select _si
_timeRearm = 5
? _unit != _vehicleRearm: _timeRearm = 30
? (_type in vDoubleRearmTime): _timeRearm = 30*2
? (_type in vTripleRearmTime): _timeRearm = 30*3
? (_type in vSixfoldRearmTime): _timeRearm = 30*6
? (_type in vTenfoldRearmTime): _timeRearm = 30*10
? dev: _timeRearm = 5
? bool_TZK_MF_Mode && _timeRearm > 30: _timeRearm = 30

_rearmData = _vehicleRearm call funcGetRearmData
_weapons = _rearmData select 0
_mags = _rearmData select 1
? count _mags == 0: exit

_moneyComm = groupMoneyMatrix select _si select (giCO select _si)
? _nonAIgroup: goto "SkipGetMoney"
; CALC COST
? _type == -1 && _unit != _vehicleRearm: format["ERROR: _type is -1 for vehicle '%1' in AI_Rearm", _vehicleRearm] call fDebugLog; exit
_money = groupMoneyMatrix select _si select _gi
# SkipGetMoney
_insufficientFond = {_this > _money && (!_bUseCommMoney || _this > _moneyComm)}
? _unit != _vehicleRearm: goto "RearmVehicle"

#RearmInfantry
	? !_nonAIgroup: _cost = [_unit, _weapons, _mags] call funcCalcRearmCost, if(_cost call _insufficientFond) Then {exit}
	
	? !(local _vehicleRearm): exit
	_vehicleRearm setCombatMode "BLUE"
	~_timeRearm
	? !(alive _vehicleRearm): _vehicleRearm setCombatMode "YELLOW"; exit
	? !(alive _support): _vehicleRearm setCombatMode "YELLOW"; exit
	removeAllWeapons _vehicleRearm
	; add custom throw/put
	{ _vehicleRearm addWeapon _x } forEach weaponsCustom
	? count (magazines _vehicleRearm) > 0: format["ERROR: soldier '%1-%2' has mags in AI_Rearm.sqs", _vehicleRearm, typeOf _vehicleRearm] call fDebugLog; _vehicleRearm setCombatMode "YELLOW"; exit
	{ _vehicleRearm addMagazine _x } foreach _mags
	? count (weapons _vehicleRearm) > 0: format["ERROR: soldier '%1-%2' has weapons in AI_Rearm.sqs", _vehicleRearm, typeOf _vehicleRearm] call fDebugLog; _vehicleRearm setCombatMode "YELLOW"; exit
	{ _vehicleRearm addWeapon _x } foreach _weapons
	_vehicleRearm setCombatMode "YELLOW"
	; _vehicleRearm selectWeapon (primaryWeapon _vehicleRearm)
	goto "RearmEnd"

#RearmVehicle
	? !_nonAIgroup: _cost = if (_type != -1) Then {(unitDefs select _type select udCost)/10} Else {50}, if (_cost < 50) Then {_cost = 50}, _cost = _cost - (_cost % 1), if (_cost call _insufficientFond) Then {exit}

	if (local _vehicleRearm) then { { _vehicleRearm removeMagazines _x } foreach (magazines _vehicleRearm) } else { [_vehicleRearm] exec "\TZK_Scripts_4_0_4\Common\SendVehicleMagsRemove.sqs" }
	~_timeRearm
	? !(alive _vehicleRearm) || !(alive _support): exit
	? count (magazines _vehicleRearm) == 0: [_vehicleRearm] exec "\TZK_Scripts_4_0_4\Common\Msg\sRearmVehGeneral.sqs"
	goto "RearmEnd"

#RearmEnd
	? _cost > 0 && _cost <= _money: [_si, _gi, _cost] exec "\TZK_Scripts_4_0_4\Server\MoneySpend.sqs", exit
	? _cost > 0: [_si, giCO select _si, _cost] exec "\TZK_Scripts_4_0_4\Server\MoneySpend.sqs", exit