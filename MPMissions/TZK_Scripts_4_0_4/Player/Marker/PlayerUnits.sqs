; args: [si, _boole_Mark_Player_Name]
_si = _this select 0; _boole_Mark_Player_Name = _this select 1

; Wait until AI leaders identified.
@(time > 10)

#Update
	? _time > 30: [_si, _boole_Mark_Player_Name] exec "\TZK_Scripts_4_0_4\Player\Marker\PlayerUnits.sqs"; exit
	? !([_boole_Mark_Player_Name, boole_Mark_Player_Name] call funcBoolEqual): goto "ResetLeaderMarker"
	_delay = if !bool_TZK_Marker_LowFrequency Then {0.5} Else {3}
	~ _delay
	? 0 == count ([_si, stradar] call funcGetWorkingStructures) || bool_TZK_Marker_Hide: call funcHideMarkers; goto "HideSleep"
	
	? bool_TZK_Marker_SQS: [_si, _boole_Mark_Player_Name] exec "\TZK_Scripts_4_0_4\Player\Marker\PlayerUnitsSQSMode.sqs"; exit
	call funcMappingUnitsPlayer
	? dev: call funcMappingPlayerDev
	
	goto "Update"
#HideSleep
	~0.5
	? 0 == count ([_si, stradar] call funcGetWorkingStructures) || bool_TZK_Marker_Hide: goto "HideSleep"
	goto "Update"
	
#ResetLeaderMarker
	_boole_Mark_Player_Name = boole_Mark_Player_Name
	? bool_TZK_199_Mode: goto "Update"

	_i = 0; _c = count (groupMatrix select _si)
	#ResetMarker
		_marker = Format ["%1%2", groupNameMatrix select _si select _i, _si];
		? !( (groupMatrix select _si select _i) in (groupAiMatrix select _si) ) : _marker setMarkerText format ["%1", [groupNameMatrix select _si select _i, name leader (groupMatrix select _si select _i) + "(" + (groupNameMatrix select _si select _i) + ")"] select boole_Mark_Player_Name]; _marker setMarkerColor (["ColorBlue", "ColorBlue"] select boole_Mark_Player_Name)
		? (groupMatrix select _si select _i) in (groupAiMatrix select _si) : _marker setMarkerText format ["%1", groupNameMatrix select _si select _i]
		_i = _i + 1
		? (_i < _c):goto "ResetMarker"
	goto "Update"
	
	
; EOF